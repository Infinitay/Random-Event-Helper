plugins {
	id 'java'
}

repositories {
	mavenLocal()
	maven {
		url = 'https://repo.runelite.net'
		content {
			includeGroupByRegex("net\\.runelite.*")
		}
	}
	mavenCentral()
}

def runeLiteVersion = 'latest.release'

dependencies {
	compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Credit to Dink Plugin
    // max version before junit-bom was added to pom files, due to runelite restrictions
    def junitVersion = "5.5.2"

    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: junitVersion
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-params", version: junitVersion
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion
    testRuntimeOnly group: "org.junit.platform", name: "junit-platform-launcher", version: "1.5.2"

    // For RelationshipSystem testing - specifically for list comparisons
    testImplementation 'org.assertj:assertj-core:3.27.6'

    testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
	testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
}

test {
    useJUnitPlatform()
}

group = 'randomeventhelper'
version = '3.2.1'

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.release.set(11)
}

tasks.register('shadowJar', Jar) {
	dependsOn configurations.testRuntimeClasspath
	manifest {
		attributes('Main-Class': 'randomeventhelper.RandomEventHelperPluginTest.java', 'Multi-Release': true)
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from sourceSets.main.output
	from sourceSets.test.output
	from {
		configurations.testRuntimeClasspath.collect { file ->
			file.isDirectory() ? file : zipTree(file)
		}
	}

	exclude 'META-INF/INDEX.LIST'
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
	exclude '**/module-info.class'

	group = BasePlugin.BUILD_GROUP
	archiveClassifier.set('shadow')
	archiveFileName.set("${rootProject.name}-${project.version}-all.jar")
}
